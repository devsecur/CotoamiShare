plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs"
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.github.triplet.play' version '2.0.0'
}

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "com.ricknout.rugbyranker"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1106
        versionName "1.1.6"
        setProperty("archivesBaseName", applicationId + "-v" + versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            // System.getenv() gets the environment variables from the CircleCI machine
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks") // Files need to be decoded in the config.yml file
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    appIcon:"@mipmap/ic_launcher",
                    appName:"@string/app_name",
                    workManagerInitAuthorities:"com.ricknout.rugbyranker.workmanager-init"
            ]
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    appIcon:"@mipmap/ic_launcher_debug",
                    appName:"@string/app_name_debug",
                    workManagerInitAuthorities:"com.ricknout.rugbyranker.debug.workmanager-init"
            ]
        }
    }
}

// Deployment to Google Play configuration for com.github.triplet.play
play {
    track = 'internal'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "rxstarter_pk.json") // Private Key is decoded from CircleCi environment variables
    defaultToAppBundles = true
    resolutionStrategy = "ignore" //This means the build will pass even if it doesn't deploy successfully. Deployment will fail if the versionCode does not change.
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:25.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    testImplementation "junit:junit:$test_junit_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
}
